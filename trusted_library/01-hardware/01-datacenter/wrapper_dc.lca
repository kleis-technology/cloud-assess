/*
Equipments (server, network, storage ...) not assigned to clients
but necessary to run the client equipments are assembled into a "internal" pool.

In the PCR, this corresponds to the "mutualized" pool of network equipments in UF2.

The internal equipment impacts are embedded using the process wrapper_dc
that should be used by upper-level functional units.
*/


/*
ENTRYPOINT
*/

process wrapper_dc {
    params {
        id = "dc-01"
    }
    products {
        1 kWh datacenter
    }
    inputs {
        1 kWh datacenter from wrapper_dc_fn( id = id, lc_step = "manufacturing" )
        1 kWh datacenter from wrapper_dc_fn( id = id, lc_step = "transport" )
        1 kWh datacenter from wrapper_dc_fn( id = id, lc_step = "use" )
        1 kWh datacenter from wrapper_dc_fn( id = id, lc_step = "end-of-life" )
    }
}

/*
PER LC STEP
*/

process wrapper_dc_fn {
    params {
        id = "dc-01"
        lc_step = "manufacturing"
    }
    variables {
        data = lookup dc_inventory match id = id
    }
    products {
        data.lifespan * data.reserved_power datacenter
    }

    inputs {
        // embed original datacenter
        data.lifespan * data.reserved_power datacenter from datacenter match ( lc_step = lc_step )
    }

    impacts {
        // impacts from internal equipments
        for_each hw_data from hw_inventory match ( dc_id = id, pool_id = "internal" ) {
            for_each impact from hw_impacts match ( id = hw_data.id, lc_step = lc_step ) {
                impact.GWP GWP
            }
        }
    }
}

