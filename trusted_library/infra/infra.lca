process pool_dedicated {
    products {
        1 p pool_dedicated
    }
    inputs {
        for_each row from inventory {
            row.n_items * 1 hour server from dedicated_server(id = row.id)
        }
    }
}

process shared_server {
    params {
        id = "small"
    }
    variables {
        row = lookup inventory match id = id
    }
    products {
        1 hour * row.ram_size ram allocate row.ram_allocation
        1 hour * row.storage_size storage allocate row.storage_allocation
    }
    inputs {
        1 hour server from dedicated_server(id = id)
    }
}

process pool_ram {
    products {
        sum(inventory, n_items * ram_size) * 1 hour ram
    }
    inputs {
        for_each row from inventory {
            row.n_items  * row.ram_size * 1 hour ram from shared_server(id = row.id)
        }
    }
}

process pool_storage {
    products {
        sum(inventory, n_items * storage_size) * 1 hour storage
    }
    inputs {
        for_each row from inventory {
            row.storage_size * 1 hour storage from shared_server(id = row.id)
        }
    }
}
